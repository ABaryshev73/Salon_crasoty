

Процедура ОбработкаПроведения(Отказ, Режим)
	
	СтруктураСпособУчетаЗапасов = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата);
	
	Если СтруктураСпособУчетаЗапасов.СпособУчетаЗапасов = Перечисления.СпособСписанияЗапасов.FEFO Тогда 
		ОтражатьСрокГодности = Истина;
	Иначе 
		ОтражатьСрокГодности = Ложь;
	КонецЕсли;
	
	Движения.ТоварыНаСкладах.Записывать = Истина;
	Движения.Хозрасчетный.Записывать = Истина;
	Движения.УчётЗатрат.Записывать = Истина;
	СчетКредита = ПланыСчетов.Хозрасчетный.РасчётыСПоставщиками;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПоступлениеТоваровИУслугУслуги.Ссылка КАК Регистратор,
	|	ПоступлениеТоваровИУслугУслуги.СтатьяЗатрат КАК СтатьяЗатрат,
	|	ПоступлениеТоваровИУслугУслуги.Услуга.СчетБухгалтерскогоУчета КАК СчетБухгалтерскогоУчета,
	|	СУММА(ПоступлениеТоваровИУслугУслуги.Сумма) КАК Сумма
	|ИЗ
	|	Документ.ПоступлениеТоваровИУслуг.Услуги КАК ПоступлениеТоваровИУслугУслуги
	|ГДЕ
	|	ПоступлениеТоваровИУслугУслуги.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеТоваровИУслугУслуги.СтатьяЗатрат,
	|	ПоступлениеТоваровИУслугУслуги.Ссылка,
	|	ПоступлениеТоваровИУслугУслуги.Услуга.СчетБухгалтерскогоУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровИУслугТовары.Номенклатура КАК Номенклатура,
	|	ПоступлениеТоваровИУслугТовары.СрокГодности КАК СрокГодности,
	|	СУММА(ПоступлениеТоваровИУслугТовары.Количество) КАК Количество,
	|	СУММА(ПоступлениеТоваровИУслугТовары.Сумма) КАК Сумма,
	|	ПоступлениеТоваровИУслугТовары.Номенклатура.СчетБухгалтерскогоУчета КАК СчетБухгалтерскогоУчета
	|ИЗ
	|	Документ.ПоступлениеТоваровИУслуг.Товары КАК ПоступлениеТоваровИУслугТовары
	|ГДЕ
	|	ПоступлениеТоваровИУслугТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеТоваровИУслугТовары.Номенклатура,
	|	ПоступлениеТоваровИУслугТовары.СрокГодности,
	|	ПоступлениеТоваровИУслугТовары.Номенклатура.СчетБухгалтерскогоУчета";
	
	РезультатаПакета = Запрос.ВыполнитьПакет();
	
	//Движения СтатьяЗатрат
	Выборка = РезультатаПакета[0].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока Выборка.Следующий() Цикл 
		
		Движение = Движения.УчётЗатрат.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		Движение.Период = Дата;
		
		//Регистр Журнал бухгалтерских проводок
		Движение = Движения.Хозрасчетный.Добавить();
		Движение.СчетДт = Выборка.СчетБухгалтерскогоУчета;
		Движение.СчетКт = СчетКредита;
		Движение.Период = Дата;
		Движение.Сумма = Выборка.Сумма;
		Движение.Содержание = "Отражено поступление услуг от поставщика";
		БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(Движение.СчетДт, Движение.СубконтоДт, Выборка.СтатьяЗатрат);
		БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(Движение.СчетКт, Движение.СубконтоКт, Контрагенты);
		
	КонецЦикла;
	
	//ДвиженияТоварыНаСклаадах
	Выборка = РезультатаПакета[1].Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		Движение = Движения.ТоварыНаСкладах.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Склад = Склад;
		Движение.Количество = Выборка.Количество;
		Движение.Сумма = Выборка.Сумма;
		
		Если ОтражатьСрокГодности Тогда 
			Движение.СрокГодности = Выборка.СрокГодности;
		КонецЕсли;
		
		//Регистр Журнал бухгалтерских проводок
		Движение = Движения.Хозрасчетный.Добавить();
		Движение.СчетДт = Выборка.СчетБухгалтерскогоУчета;
		Движение.СчетКт = СчетКредита;
		Движение.Период = Дата;
		Движение.Сумма = Выборка.Сумма;
		Движение.Содержание = "Отражено поступление товарно-материальных ценностей от поставщика";
		БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(Движение.СчетДт, Движение.СубконтоДт, Выборка.Номенклатура);
		БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(Движение.СчетКт, Движение.СубконтоКт, Контрагенты);
		
	КонецЦикла;
	
	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
КонецПроцедуры

